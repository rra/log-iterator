#!/usr/bin/perl
#
# Test Perl code for strict, warnings, syntax, and test coverage.
#
# Test coverage checking is disabled unless RRA_MAINTAINER_TESTS is set since
# it takes a long time, is sensitive to the versions of various libraries,
# and will not interfere with functionality.
#
# Written by Russ Allbery <rra@stanford.edu>
# Copyright 2013
#     The Board of Trustees of the Leland Stanford Junior University
#
# See LICENSE for licensing terms.

use strict;
use warnings;

use File::Spec;
use Test::More;

# Coverage level achieved.
use constant COVERAGE_LEVEL => 100;

# Non-fuctional, generic tests to skip for test coverage analysis.  Also skip
# Build since Module::Build doesn't use strict.
use constant COVERAGE_SKIP_TESTS => (
    't/critic.t',       't/minimum-version.t',
    't/pod-coverage.t', 't/pod-spelling.t',
    't/pod.t',          't/synopsis.t',
);

# Skip tests if Test::Strict is not installed.
if (!eval { require Test::Strict }) {
    plan skip_all => 'Test::Strict required to test Perl syntax';

    # Suppress "only used once" warnings.
    $Test::Strict::TEST_SKIP           = [];
    $Test::Strict::TEST_WARNINGS       = 0;
    $Test::Strict::DEVEL_COVER_OPTIONS = q{};
}
Test::Strict->import;

# Build the path to the Build script so that we can ignore it.  This is a
# hack; the internal coverage code in Test::Strict builds paths in a very
# strange way that we have to match to ignore things.
my $build_path = File::Spec->catfile('t', File::Spec->updir, 'Build');
$build_path = File::Spec->rel2abs($build_path);

# Test everything in the distribution directory except the Build script
# generated by Module::Build.  We also want to check use warnings.
$Test::Strict::TEST_SKIP     = [$build_path];
$Test::Strict::TEST_WARNINGS = 1;
all_perl_files_ok();

# Test code coverage.  Skip this test unless we're running the test suite in
# maintainer mode.
SKIP: {
    if (!$ENV{RRA_MAINTAINER_TESTS}) {
        skip 'Coverage test only run for maintainer', 1;
    }
    if (!eval { require Devel::Cover }) {
        skip 'Devel::Cover required to check test coverage', 1;
    }

    # Skip the various generic tests that don't add to coverage.  Some of
    # these load a lot of Perl code, which causes Devel::Cover to be very
    # slow.
    my @skip = map { File::Spec->canonpath($_) } COVERAGE_SKIP_TESTS;
    $Test::Strict::TEST_SKIP = [@skip];

    # Disable POD coverage; that's handled separately and is confused by our
    # autoloading.
    $Test::Strict::DEVEL_COVER_OPTIONS
      = '-coverage,statement,branch,condition,subroutine';
    all_cover_ok(COVERAGE_LEVEL);
}
